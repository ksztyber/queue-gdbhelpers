source queue.py

define print_status
	printf "%-40s %s\n", $arg0 ":", "$arg1"
end

define test_outer
	queue-size $arg0_head link
	if $ != MAX_ITEMS
		print_status "$arg0_head size" FAIL
		set $rc = -1
	end
	queue-size $arg0_single_head link
	if $ != 1
		print_status "$arg0_single_head size" FAIL
		set $rc = -1
	end
	queue-size $arg0_empty_head link
	if $ != 0
		print_status "$arg0_empty_head size" FAIL
		set $rc = -1
	end

	set $idx = 0
	while $idx < MAX_ITEMS
		queue-at $arg0_head link $idx
		if $.id != $idx + 1
			print_status "$arg0_head at" FAIL
			set $idx = MAX_ITEMS
			set $rc = -1
		end

		set $idx = $idx + 1
	end

	# These foreach tests assume MAX_ITEMS == 128
	set $idx = 0
	queue-foreach $arg0_head link
	while $idx < MAX_ITEMS
		set $val = $$127.id
		if $val != $idx + 1
			print_status "$arg0_head foreach" FAIL
			set $idx = MAX_ITEMS
			set $rc = -1
		end

		set $idx = $idx + 1
		# Print the value to advance $$, so that a constant 127 can be used
		print $val
	end

	set $idx = 0
	queue-foreach $arg0_head link id
	while $idx < MAX_ITEMS
		set $val = $$127
		if $val != $idx + 1
			print_status "$arg0_head foreach id" FAIL
			set $idx = MAX_ITEMS
			set $rc = -1
		end

		set $idx = $idx + 1
		print $val
	end
end

define test_inner
	queue-size $arg0_inner_head inner.link
	if $ != MAX_ITEMS
		print_status "$arg0_inner_head size" FAIL
		set $rc = -1
	end
	queue-size $arg0_inner_single_head inner.link
	if $ != 1
		print_status "$arg0_inner_single_head size" FAIL
		set $rc = -1
	end
	queue-size $arg0_inner_empty_head inner.link
	if $ != 0
		print_status "$arg0_inner_empty_head size" FAIL
		set $rc = -1
	end

	set $idx = 0
	while $idx < MAX_ITEMS
		queue-at $arg0_inner_head inner.link $idx
		if $.id != $idx + 1
			print_status "$arg0_inner_head at" FAIL
			set $idx = MAX_ITEMS
			set $rc = -1
		end

		set $idx = $idx + 1
	end

	set $idx = 0
	queue-foreach $arg0_inner_head inner.link id
	while $idx < MAX_ITEMS
		set $val = $$127
		if $val != $idx + 1
			print_status "$arg0_inner_head foreach id" FAIL
			set $idx = MAX_ITEMS
			set $rc = -1
		end

		set $idx = $idx + 1
		# Print the value to advance $$, so that a constant 127 can be used
		print $val
	end
end

define test_queue
	set $rc = 0
	test_outer $arg0
	test_inner $arg0
	if $rc == 0
		print_status "$arg0" OK
	end
end

define test_tree
	set $rc = 0
	test_outer $arg0
	if $rc == 0
		print_status "$arg0" OK
	end
end

set pagination off

b main
commands 1
	test_queue LIST
	test_queue SLIST
	test_queue SIMPLEQ
	test_queue TAILQ
	test_queue STAILQ
	test_queue CIRCLEQ
	test_tree SPLAY
	test_tree RB
end

run
